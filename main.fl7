@STRICT;

index : [
  [0, 3],
  [1, 4],
  [2, 5],
  [6, 7],
];

{

  encode = table -> (
    &(
      table[] => SLICE >> 4 | (
        list_tenji : [0 ~ CEIL((_[] | $#_ => MAX) / 2) | [0 ~ 8 | 0]];
        r, _ : _[] | (
          c, _ : _[] | (
            _ && (
              list_tenji[c `DIV` 2][index[r][c % 2]] = 1;
            );
          );
        );
        FROM_CHARCODES([list_tenji[] | H#2800 + _.0 * 1 + _.1 * 2 + _.2 * 4 + _.3 * 8 + _.4 * 16 + _.5 * 32 + _.6 * 64 + _.7 * 128])
      )
    )
  );

  // experimental
  encode_colored = table, colorMap -> (
    &(
      table[] => SLICE >> 4 | (
        list_tenji : [0 ~ CEIL((_[] | $#_ => MAX) / 2) | [0 ~ 8 | NULL]];
        r, _ : _[] | (
          c, _ : _[] | (
            list_tenji[c `DIV` 2][index[r][c % 2]] = colorMap[+_];
          );
        );
        JOIN(list_tenji[] | (
          color : ([_[] | _ ?: ()] | $#_ === 0 ? "" : (_[] => GROUP => SORTR_BY >> \$#_ => FIRST | _.0));
          char : FROM_CHARCODES([+(
            H#2800,
            _.0 !== NULL ? 1 : 0,
            _.1 !== NULL ? 2 : 0,
            _.2 !== NULL ? 4 : 0,
            _.3 !== NULL ? 8 : 0,
            _.4 !== NULL ? 16 : 0,
            _.5 !== NULL ? 32 : 0,
            _.6 !== NULL ? 64 : 0,
            _.7 !== NULL ? 128 : 0,
          )]);
          color & char
        ); "")
      )
    )
  );

}
